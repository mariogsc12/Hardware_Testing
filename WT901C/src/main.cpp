#include <HardwareSerial.h>
#include "wit_c_sdk.h"

/*
Visit: https://wit-motion.gitbook.io/witmotion-sdk/wit-standard-protocol/sdk/arduino_sdk-quick-start

Test on ESP32. use WT901CTTL sensor

WT901CTTL     ESP32 Dev Module
    VCC <--->  5V/3.3V
    TX  <--->  RX2 (GPIO16)
    RX  <--->  TX2 (GPIO17)
    GND <--->  GND
*/
#define RXD2 16
#define TXD2 17

#define ACC_UPDATE    0x01
#define GYRO_UPDATE   0x02
#define ANGLE_UPDATE  0x04
#define MAG_UPDATE    0x08
#define READ_UPDATE   0x80
static volatile char s_cDataUpdate = 0, s_cCmd = 0xff;

static void CmdProcess(void);
static void AutoScanSensor(void);
static void SensorUartSend(uint8_t *p_data, uint32_t uiSize);
static void SensorDataUpdate(uint32_t uiReg, uint32_t uiRegNum);
static void Delayms(uint16_t ucMs);

const uint32_t c_uiBaud[8] = {0, 4800, 9600, 19200, 38400, 57600, 115200, 230400};

int i;
float fAcc[3], fGyro[3], fAngle[3];

void CopeCmdData(unsigned char ucData) {
  static unsigned char s_ucData[50], s_ucRxCnt = 0;
  
  s_ucData[s_ucRxCnt++] = ucData;
  if (s_ucRxCnt < 3) return; // Less than three data returned
  if (s_ucRxCnt >= 50) s_ucRxCnt = 0;

  if (s_ucRxCnt >= 3) {
    if ((s_ucData[1] == '\r') && (s_ucData[2] == '\n')) {
      s_cCmd = s_ucData[0];
      memset(s_ucData, 0, 50);
      s_ucRxCnt = 0;
    } else {
      s_ucData[0] = s_ucData[1];
      s_ucData[1] = s_ucData[2];
      s_ucRxCnt = 2;
    }
  }
}

static void ShowHelp(void) {
  Serial.print("\r\n************************  WIT_SDK_DEMO  ************************");
  Serial.print("\r\n************************       HELP         ************************\r\n");
  Serial.print("UART SEND:a\\r\\n   Acceleration calibration.\r\n");
  Serial.print("UART SEND:m\\r\\n   Magnetic field calibration. Send e\\r\\n to indicate end.\r\n");
  Serial.print("UART SEND:U\\r\\n   Increase bandwidth.\r\n");
  Serial.print("UART SEND:u\\r\\n   Decrease bandwidth.\r\n");
  Serial.print("UART SEND:B\\r\\n   Set baud rate to 115200.\r\n");
  Serial.print("UART SEND:b\\r\\n   Set baud rate to 9600.\r\n");
  Serial.print("UART SEND:R\\r\\n   Set return rate to 10Hz.\r\n");
  Serial.print("UART SEND:r\\r\\n   Set return rate to 1Hz.\r\n");
  Serial.print("UART SEND:C\\r\\n   Set return content: acc, gyro, angle, mag.\r\n");
  Serial.print("UART SEND:c\\r\\n   Set return content: acceleration.\r\n");
  Serial.print("UART SEND:h\\r\\n   Help.\r\n");
  Serial.print("***************************************************************\r\n");
}

static void CmdProcess(void) {
  switch (s_cCmd) {
    case 'a': if (WitStartAccCali() != WIT_HAL_OK) Serial.print("\r\nSet AccCali Error\r\n"); break;
    case 'm': if (WitStartMagCali() != WIT_HAL_OK) Serial.print("\r\nSet MagCali Error\r\n"); break;
    case 'e': if (WitStopMagCali() != WIT_HAL_OK) Serial.print("\r\nSet MagCali Error\r\n"); break;
    case 'u': if (WitSetBandwidth(BANDWIDTH_5HZ) != WIT_HAL_OK) Serial.print("\r\nSet Bandwidth Error\r\n"); break;
    case 'U': if (WitSetBandwidth(BANDWIDTH_256HZ) != WIT_HAL_OK) Serial.print("\r\nSet Bandwidth Error\r\n"); break;
    case 'B': 
      if (WitSetUartBaud(WIT_BAUD_115200) != WIT_HAL_OK) Serial.print("\r\nSet Baud Error\r\n");
      else {
        Serial2.updateBaudRate(115200);
        Serial.print("115200 Baud rate modified successfully\r\n");
      }
      break;
    case 'b': 
      if (WitSetUartBaud(WIT_BAUD_9600) != WIT_HAL_OK) Serial.print("\r\nSet Baud Error\r\n");
      else {
        Serial2.updateBaudRate(9600);
        Serial.print("9600 Baud rate modified successfully\r\n");
      }
      break;
    case 'r': 
      if (WitSetOutputRate(RRATE_1HZ) != WIT_HAL_OK) Serial.print("\r\nSet Output Rate Error\r\n");
      else Serial.print("\r\nSet Output Rate Success\r\n");
      break;
    case 'R': 
      if (WitSetOutputRate(RRATE_10HZ) != WIT_HAL_OK) Serial.print("\r\nSet Output Rate Error\r\n");
      else Serial.print("\r\nSet Output Rate Success\r\n");
      break;
    case 'C': 
      if (WitSetContent(RSW_ACC | RSW_GYRO | RSW_ANGLE | RSW_MAG) != WIT_HAL_OK) Serial.print("\r\nSet RSW Error\r\n"); 
      break; 
    case 'c': 
      if (WitSetContent(RSW_ACC) != WIT_HAL_OK) Serial.print("\r\nSet RSW Error\r\n"); 
      break; 
    case 'h': 
      ShowHelp(); 
      break; 
    default: 
      break; 
  } 
  s_cCmd = 0xff; 
}

static void SensorUartSend(uint8_t *p_data, uint32_t uiSize) {
  Serial2.write(p_data, uiSize); 
  Serial2.flush(); 
}

static void Delayms(uint16_t ucMs) {
  delay(ucMs); 
}

static void SensorDataUpdate(uint32_t uiReg, uint32_t uiRegNum) {
  int i; 
  for (i = 0; i < uiRegNum; i++) {
    switch (uiReg) {
      case AZ: 
        s_cDataUpdate |= ACC_UPDATE; 
        break; 
      case GZ: 
        s_cDataUpdate |= GYRO_UPDATE; 
        break; 
      case HZ: 
        s_cDataUpdate |= MAG_UPDATE; 
        break; 
      case Yaw: 
        s_cDataUpdate |= ANGLE_UPDATE; 
        break; 
      default: 
        s_cDataUpdate |= READ_UPDATE; 
        break; 
    } 
    uiReg++; 
  } 
}

static void AutoScanSensor(void) {
  int i, iRetry; 
  
  for (i = 0; i < sizeof(c_uiBaud) / sizeof(c_uiBaud[0]); i++) {
    Serial2.begin(c_uiBaud[i], SERIAL_8N1, RXD2, TXD2); 
    Serial2.flush(); 
    iRetry = 2; 
    s_cDataUpdate = 0; 
    do {
      WitReadReg(AX, 3); 
      delay(200); 
      while (Serial2.available()) {
        WitSerialDataIn(Serial2.read()); 
      }
      if (s_cDataUpdate != 0) {
        Serial.print(c_uiBaud[i]); 
        Serial.print(" baud sensor detected\r\n\r\n"); 
        ShowHelp(); 
        return; 
      }
      iRetry--; 
    } while (iRetry); 
  } 
  Serial.print("Cannot find sensor\r\n"); 
  Serial.print("Please check your connections\r\n"); 
}

// ---------------------------------- //
// ---------------------------------- //
// ---------------------------------- //



void setup() {
  Serial.begin(115200);
  Serial.print("\r\n********************** wit-motion normal example  ************************\r\n");

  Serial2.begin(115200, SERIAL_8N1, RXD2, TXD2);
  Serial.print("Serial2 ready\r\n");

  WitInit(WIT_PROTOCOL_NORMAL, 0x50);
  WitSerialWriteRegister(SensorUartSend);
  WitRegisterCallBack(SensorDataUpdate);
  WitDelayMsRegister(Delayms);

  if (WitStartAccCali() == WIT_HAL_OK) {
    Serial.print("Acc calibration OK\r\n");
  } else {
    Serial.print("Acc calibration Incorrect\r\n");
  }

  if (WitStartMagCali() == WIT_HAL_OK) {
    Serial.print("Mag calibration started. Move sensor in figure-eight motion.\r\n");
    delay(10000); 
    if (WitStopMagCali() == WIT_HAL_OK) {
      Serial.print("Mag calibration completed successfully\r\n");
    } else {
      Serial.print("Mag calibration failed\r\n");
    }
  } else {
    Serial.print("Mag calibration Incorrect\r\n");
  }

  AutoScanSensor();
}



void loop() {
  // Read sensor data from Serial2
  while (Serial2.available()) {
    WitSerialDataIn(Serial2.read());
  }
  // Handle commands from Serial input (if any)
  while (Serial.available()) {
    CopeCmdData(Serial.read());
  }

  CmdProcess();

  if (s_cDataUpdate) {
    for (i = 0; i < 3; i++) {
      fAcc[i] = sReg[AX + i] / 32768.0f * 16.0f;
      fGyro[i] = sReg[GX + i] / 32768.0f * 2000.0f;
      fAngle[i] = sReg[Roll + i] / 32768.0f * 180.0f;
    }

    if (s_cDataUpdate & ACC_UPDATE) {
      Serial.print("acc: ");
      Serial.print(fAcc[0], 3);
      Serial.print(" ");
      Serial.print(fAcc[1], 3);
      Serial.print(" ");
      Serial.print(fAcc[2], 3);
      Serial.print("\r\n");
      s_cDataUpdate &= ~ACC_UPDATE;
    }

    if (s_cDataUpdate & GYRO_UPDATE) {
      Serial.print("gyro: ");
      Serial.print(fGyro[0], 1);
      Serial.print(" ");
      Serial.print(fGyro[1], 1);
      Serial.print(" ");
      Serial.print(fGyro[2], 1);
      Serial.print("\r\n");
      s_cDataUpdate &= ~GYRO_UPDATE;
    }

    if (s_cDataUpdate & ANGLE_UPDATE) {
      Serial.print("angle: ");
      Serial.print(fAngle[0], 3);
      Serial.print(" ");
      Serial.print(fAngle[1], 3);
      Serial.print(" ");
      Serial.print(fAngle[2], 3);
      Serial.print("\r\n");
      s_cDataUpdate &= ~ANGLE_UPDATE;
    }

    if (s_cDataUpdate & MAG_UPDATE) {
      Serial.print("mag: ");
      Serial.print(sReg[HX]);
      Serial.print(" ");
      Serial.print(sReg[HY]);
      Serial.print(" ");
      Serial.print(sReg[HZ]);
      Serial.print("\r\n");
      s_cDataUpdate &= ~MAG_UPDATE;
    }

    s_cDataUpdate = 0;
  }
  delay(1000);
}

